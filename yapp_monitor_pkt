//Monitor

typedef enum bit {COV_ENABLE, COV_DISABLE} cover_e;

class yapp_tx_monitor extends uvm_monitor;

  yapp_packet pkt;

  int num_pkt_col;
    //Connecting mon to Sb by using TLM Port
  uvm_analysis_port#(yapp_packet) item_collected_port;
  cover_e coverage_control = COV_ENABLE;
  virtual interface yapp_if vif;

   //Provide implementations of virtual methods such as get_type_name and create

  `uvm_component_utils_begin(yapp_tx_monitor)
    `uvm_field_int(num_pkt_col, UVM_ALL_ON)
    `uvm_field_enum(cover_e, coverage_control, UVM_ALL_ON)
  `uvm_component_utils_end

  covergroup collected_pkts_cg;
    option.per_instance=1;
    REQ1_length: coverpoint pkt.length {
      bins MIN = {1};
      bins SMALL = {[2:10]};
      bins MEDIUM = {[11:40]};
      bins LARGE = {[41:62]};
      bins MAX = {63};
    }

    REQ2_addr : coverpoint pkt.addr {
      bins addr[] = {[0:2]};
      bins illegal = {3};
    }

    bad_parity: coverpoint pkt.parity_type {
      bins bad = {BAD_PARITY};
      bins good = default;
    }

    REQ3_cross_addr_length: cross REQ1_length, REQ2_addr;
    REQ3_cross_addr_bad_parity: cross  REQ2_addr, bad_parity;
  endgroup: collected_pkts_cg

  function new (string name, uvm_component parent);
    super.new(name, parent);
    item_collected_port = new("item_collected_port",this);
    if (coverage_control == COV_ENABLE) begin
     `uvm_info(get_type_name(),"YAPP MONITOR COVERAGE CREATED" , UVM_LOW)
      collected_pkts_cg = new();
      collected_pkts_cg.set_inst_name({get_full_name(), ".monitor_pkt"});
    end
  endfunction : new
 
  // Getting the virutal i/f Handler from config. db
  function void build_phase(uvm_phase phase);
    if (!yapp_vif_config::get(this, get_full_name(),"vif", vif))
      `uvm_error("NOVIF",{"virtual interface must be set for: ",get_full_name(),".vif"})
  endfunction: build_phase

  task run_phase(uvm_phase phase);
    @(posedge vif.reset)
    @(negedge vif.reset)
    `uvm_info(get_type_name(), "Detected Reset Done", UVM_MEDIUM)
    forever begin
      pkt = yapp_packet::type_id::create("pkt", this);

      fork
        vif.collect_packet(pkt.length, pkt.addr, pkt.payload, pkt.parity);
        @(posedge vif.monstart) void'(begin_tr(pkt, "Monitor_YAPP_Packet"));
      join

      pkt.parity_type = (pkt.parity == pkt.calc_parity()) ? GOOD_PARITY : BAD_PARITY;
      end_tr(pkt);
      `uvm_info(get_type_name(), $sformatf("Packet Collected :\n%s", pkt.sprint()), UVM_NONE)
      item_collected_port.write(pkt);
      if (coverage_control == COV_ENABLE) begin
        `uvm_info(get_type_name(),"YAPP MONITOR COVERAGE SAMPLE" , UVM_NONE)
        collected_pkts_cg.sample();
      end
      num_pkt_col++;
    end
  endtask : run_phase

  function void report_phase(uvm_phase phase);
    `uvm_info(get_type_name(), $sformatf("Report: YAPP Monitor Collected %0d Packets", num_pkt_col), UVM_LOW)
  endfunction : report_phase

endclass : yapp_tx_monitor

